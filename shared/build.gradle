import org.gradle.internal.os.OperatingSystem

apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'
apply plugin: 'kotlin-dce-js'

def ktor_version = "1.6.8"

repositories {
    mavenCentral()
}

kotlin {
    targets {
        final def iOSTarget = System.getenv('SDK_NAME')?.startsWith("iphoneos") ? presets.iosArm64 : presets.iosX64

        fromPreset(iOSTarget, 'ios') {
            binaries {
                framework()
            }
        }

        fromPreset(presets.jvm, 'android')
        fromPreset(presets.iosX64, 'iosX64')
        fromPreset(presets.iosArm64, 'iosArm64') {
            binaries {
                framework()
            }
        }
        if(OperatingSystem.current() == OperatingSystem.MAC_OS) {
            fromPreset(presets.macosX64, 'cli') {
                binaries {
                    executable()
                }
            }
        } else if(OperatingSystem.current()== OperatingSystem.LINUX) {
            fromPreset(presets.linuxX64, 'cli') {
                binaries {
                    executable()
                }
            }
        }
        fromPreset(presets.js, 'js') {
            moduleName = "shared"

            browser {
                webpackTask {
                    outputFileName = "game.min.js"
                }
            }

            compilations.main {
                configure([compileKotlinJs, compileTestKotlinJs]) {
                    kotlinOptions {
                        languageVersion = "1.5"
                        sourceMap = true
                        metaInfo = true
                        moduleKind = "umd"
                    }
                }
            }
        }

        configure([iosX64, iosArm64]) {
            // compilations.main.outputKinds("FRAMEWORK")
        }
        configure([cli]) {
            // compilations.main.outputKinds('EXECUTABLE')
        }
    }

    sourceSets {
        configure([iosX64Main, iosArm64Main]) {
            dependsOn iosMain
        }
        commonMain.dependencies {
            implementation "io.ktor:ktor-client-core:$ktor_version"
            implementation "io.ktor:ktor-client-serialization:$ktor_version"
            implementation "com.soywiz.korlibs.klock:klock:2.4.13"
            implementation "com.russhwolf:multiplatform-settings:0.8.1"
        }

        commonTest.dependencies {
            implementation(kotlin("test"))
        }

        androidMain.dependencies {
            implementation "io.ktor:ktor-client-android:$ktor_version"
        }

        iosMain.dependencies {
            implementation "io.ktor:ktor-client-ios:$ktor_version"
        }

        jsMain.dependencies {
            implementation "org.jetbrains.kotlinx:kotlinx-html-js:0.7.5"
            implementation "io.ktor:ktor-client-js:$ktor_version"
            implementation "io.ktor:ktor-client-json-js:$ktor_version"
        }

        cliMain.dependencies {
            implementation "io.ktor:ktor-client-curl:$ktor_version"
        }

        iosArm64Main.dependencies {
            // implementation "io.ktor:ktor-client-ios:$ktor_version"
        }

        iosX64Main.dependencies {
            // implementation "io.ktor:ktor-client-ios:$ktor_version"
        }

        iosTest.dependencies {
        }
    }
}

configurations {
    compileClasspath
}

//////////////////////////////////////////////////////
// IOS
//////////////////////////////////////////////////////

/*
// Cannot add task 'iosTest' as a task with that name already exists.
task iosTest {
    def device = project.findProperty("iosDevice")?.toString() ?: "iPhone 8"
    dependsOn 'linkTestDebugExecutableIos'
    group = JavaBasePlugin.VERIFICATION_GROUP
    description = "Runs tests for target 'ios' on an iOS simulator"

    doLast {
        def binary = kotlin.targets.ios.binaries.getExecutable('test', 'DEBUG').outputFile
        exec {
            commandLine 'xcrun', 'simctl', 'spawn', device, binary.absolutePath
        }
    }
}
*/

task copyFramework {
    def buildType = project.findProperty("kotlin.build.type") ?: "DEBUG"
    def bin = kotlin.targets.ios.compilations.main.target.binaries.findFramework("", buildType)
    dependsOn bin.linkTask

    doLast {
        def srcFile = kotlin.targets.ios.compilations.main.target.binaries.findFramework("", buildType).outputFile

        copy {
            from srcFile.parent
            into "$projectDir.parent/ios"
            include 'shared.framework/**'
            include 'shared.framework.dSYM'
        }
    }
}

//////////////////////////////////////////////////////
// JS
//////////////////////////////////////////////////////

def jsFiles = ["build/kotlin-js-min/js/main/kotlin.js",
"build/kotlin-js-min/js/main/kotlinx-html-js.js",
"build/kotlin-js-min/js/main/kotlinx-atomicfu.js",
"build/kotlin-js-min/js/main/kotlinx-coroutines-core.js",
"build/kotlin-js-min/js/main/kotlinx-serialization-kotlinx-serialization-core-js-legacy.js",
"build/kotlin-js-min/js/main/kotlinx-serialization-kotlinx-serialization-json-js-legacy.js",
"build/kotlin-js-min/js/main/ktor-ktor-io-js-legacy.js",
"build/kotlin-js-min/js/main/ktor-ktor-utils-js-legacy.js",
"build/kotlin-js-min/js/main/ktor-ktor-http-js-legacy.js",
"build/kotlin-js-min/js/main/ktor-ktor-http-cio-js-legacy.js",
"build/kotlin-js-min/js/main/ktor-ktor-client-core-js-legacy.js",
"build/kotlin-js-min/js/main/ktor-ktor-client-js-js-legacy.js",
"build/kotlin-js-min/js/main/ktor-ktor-client-json-js-legacy.js",
"build/kotlin-js-min/js/main/ktor-ktor-client-serialization-js-legacy.js",
"build/kotlin-js-min/js/main/klock-root-klock-js-legacy.js",
"build/kotlin-js-min/js/main/MultiplatformSettings-multiplatform-settings-js-legacy.js",
"build/kotlin-js-min/js/main/shared.js"]


task minifyJS(type: JavaExec) {
    main = '-jar'
    args = ["${projectDir}/tools/closure-compiler-v20190929.jar",
            "--js_output_file=$buildDir/webRelease/game.min.js"] + jsFiles
}

task jsWebDebug(dependsOn: [runDceJsKotlin]) {
    doLast {
        new File("$buildDir/webDebug").mkdirs()
        new File("$buildDir/webDebug/game.min.js").withWriter { w ->
            jsFiles.each { f ->
                new File( f ).withReader { r ->
                    w << r << '\n'
                }
            }
        }
        copy {
            from kotlin.sourceSets.jsMain.resources.srcDirs
            into "$buildDir/webDebug"
        }
    }
}

task jsWebRelease(dependsOn: [runDceJsKotlin]) {
    doLast {
        copy {
            from kotlin.sourceSets.jsMain.resources.srcDirs
            into "$buildDir/webRelease"
        }
    }
}

jsWebRelease.finalizedBy minifyJS
